buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.0'
        classpath 'com.google.gms:google-services:1.3.0'
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'io.fabric'

repositories {
    jcenter()
    maven { url 'https://maven.fabric.io/public' }
}


dependencies {
    //compile files('libs/org.apache.http.legacy.jar')
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.google.code.gson:gson:2.2.4'
    compile 'com.twitter:twitter-text:1.13.0'
    compile 'com.google.android.gms:play-services:8.1.0'
    compile 'com.google.android.gms:play-services-plus:8.1.0'
    compile 'com.google.android.gms:play-services-identity:8.1.0'

    //jcenter mavencentral conflict - locally imported Facebook SDK aar
    //Cardview required for Facebook SDK 4.9.0
    compile 'com.android.support:cardview-v7:24.1.0'
    compile 'com.android.support:appcompat-v7:24.2.1'
    //http://stackoverflow.com/questions/21882804/adding-local-aar-files-to-my-gradle-build
    compile project(':facebook-android-sdk-4.18.0')
    compile project(':jump')
    compile('com.twitter.sdk.android:twitter:2.3.1@aar') {
        transitive = true;
    }
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    //If building with strict Android 6.0 the following will need to be uncommented
    //See: https://developer.android.com/about/versions/marshmallow/android-6.0-changes.html
    //And: http://stackoverflow.com/questions/31653002/how-to-use-the-legacy-apache-http-client-on-android-m
    useLibrary "org.apache.http.legacy"


    defaultConfig {
        applicationId "com.janrain.android.multidex.simpledemonative"
        minSdkVersion 17
        targetSdkVersion 25
        multiDexEnabled = true

        // If using web-based (not native) Google authentication.
        // Replace the below string with your own Google client ID. Make sure this is consistent
        // with the values used in openid_appauth_idp_configs.xml
        manifestPlaceholders = [
                'appAuthRedirectScheme': 'com.googleusercontent.apps.UPDATE_WITH_GOOGLE_CLIENT_ID'
        ]
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        // Move the tests to tests/java, tests/res, etc...
        instrumentTest.setRoot('tests')

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }

}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}
